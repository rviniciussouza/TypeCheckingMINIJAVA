I -> M Cx
Cx -> C Cx
Cx -> ''
M -> class ID { public static void main ( String [ ] ID ) { Sx } }
C -> class ID { Vx Fx }
C -> class ID extends ID { Vx Fx }
Vx -> V Vx 
Vx -> ''
V -> T ID ;
Fx -> F Fx
Fx -> ''
F -> public T ID ( Px ) { Vx Sx return E ; }
Px -> T ID Pz
Px -> ''
Pz -> , T ID Pz
Pz -> ''
T -> int [ ]
T -> boolean
T -> int
T -> ID
Sx -> S Sx
Sx -> ''
S -> { Sx }
S -> if ( E ) S else S
S -> while ( E ) S
S -> System.out.println ( E ) ;
S -> ID = E ;
S -> ID [ E ] = E ;
E -> E && E
E -> E || E
E -> E < E
E -> E + E
E -> E - E
E -> E * E
E -> E [ E ]
E -> E . length
E -> E . ID ( Ex )
E -> NUM_DECIM
E -> BOOLEAN_VALUE
E -> ID
E -> this
E -> new int [ E ]
E -> new ID ( )
E -> ! E
E -> ( E )
Ex -> ''
Ex -> E Ez
Ez -> , E Ez
Ez -> ''
